import Server from "./libs/sockets-gr/server"
import Client from "./libs/sockets-gr/client"
import Array from "array"
import String from "string"
import Char from "char"
import Bytes from "bytes"
import Option from "option"
import Int32 from "int32"
import Map from "map"
import {schedYield} from "sys/process"

let _MQTT_VERSION_3_1_1 = 4
let _MQTTBROKER_MY_NUM =  0
let _MQTTBROKER_MAX_NUM = 10

let _MQTTBROKER_CLIENT_SUBSCRIPTIONS_MAX =       3
let _MQTTBROKER_MESSAGE_STACK_MAX =              5
let _MQTTBROKER_TOPIC_MAX_LENGTH =               64
let _MQTTBROKER_PAYLOAD_MAX_LENGTH =             255

// Packets
let _CONNECT = 1
let _CONNACK = 2
let _PUBLISH = 3
let _PUBACK = 4
let _PUBREC = 5 
let _PUBREL = 6
let _PUBCOMP = 7
let _SUBSCRIBE = 8
let _SUBACK = 9
let _UNSUBSCRIBE = 10
let _UNSUBACK = 11
let _PINGREQ = 12
let _PINGRESP = 13
let _DISCONNECT = 14

// Connect flags
let _CLEAN_SESSION = 0x02
let _WILL_FLAG = 0x04
let _WILL_QOS = 0x18
let _WILL_RETAIN = 0x20
let _PASSWORD_FLAG = 0x40
let _USER_NAME_FLAG = 0x80

// Sonnack sp
let _SESSION_PRESENT_ZERO = 0
let _SESSION_PRESENT_ONE = 1

// Connack rc
let _CONNECT_ACCEPTED = 0    
let _CONNECT_REFUSED_UPV = 1 
let _CONNECT_REFUSED_IR = 2  
let _CONNECT_REFUSED_SU = 3  
let _CONNECT_REFUSED_BUP = 4 
let _CONNECT_REFUSED_NA = 5  

let debug = (text) => {
    if (1 == 0) {
        print(text)
    }
}

/**
 * Mqtt subscription record
 * 
 * Defines a subscription from a client
 *
 * @since v0.1.0
 */
record MqttSubscription {
    mut status: Bool,
    mut topicName: String,
    mut topicLength: Number
}

/**
 * Mqtt client record
 *
 * Holds the fd reference, state and the subscriptions
 *
 * @since v0.1.0
 */
record MqttClient {
    mut state: Bool,
    mut socketClient: Option<Client.Client>,
    subscriptions: Map.Map<String, MqttSubscription>
}

/**
 * Mqtt broker
 *
 * The broker itself
 *
 * @since v0.1.0
 */
export record MqttBroker {
    initialize: () -> Void,
    start: () -> Void,
    loop: () -> Void,
    connect: (Number) -> Void,
    subscribe: (String) -> Void,
    unsubscribe: (String) ->Void,
    publish: (String, String, Bool) -> Void,
    onReceive: ((MqttClient, String, String) -> Void) -> Void,
}

/**
 * Create new broker instance
 *
 * @returns Mqtt broker instance
 *
 * @example new()
 * 
 * @since v0.1.0
 * 
 */
export let new = () => {
    let mut callbackEnabled = false
    let socketServer = Server.new(0, 10)
    let mqttClients = Map.make()

    let mut cbReceive = (cb: client: MqttClient,topic: String, message: String) => {void}
    
    let msbLsb = (byte) => {
        (byte <<8) + (byte+1)
    }

    let compare = (topic: String, subscription: MqttSubscription) => {
        let mut cs = 0
        let mut ct = 0
        let mut result = false
        let length = String.length(topic)

        for (ct = 0; ct < length; ct += 1) {

            if (cs >= subscription.topicLength) {
                result = false
                break
            } 
            if (String.charAt(ct, topic) == String.charAt(cs, subscription.topicName)) {
                cs += 1
            } else {
                if (String.charAt(cs, subscription.topicName) == Char.fromCode(0x2B)) {
                    if ((String.charAt(ct, topic) == Char.fromCode(0x2F)) && (cs < subscription.topicLength - 1) && (String.charAt(cs+1, subscription.topicName) == Char.fromCode(0x2F))) {
                            cs += 2
                        }
                } else if (String.charAt(cs, subscription.topicName) == Char.fromCode(0x23)) {
                    result = true
                    break
                } else {
                    result = false
                    break
                }
            }
        }

        if (!result) {
            if ((String.charAt(cs,subscription.topicName) == Char.fromCode(0x2B)) && (cs == subscription.topicLength -1)) {
                result = true
            }
            else if (cs < subscription.topicLength) {
                result = false
            } 
            else if (cs == subscription.topicLength) {
                result = true
            } else {
                result= false
            }
        }

        result
    }

    let subscribeInternal = (num: Number, topic: String, length: Number) => {
        let client = Map.get(num, mqttClients)
        if (Option.isSome(client)) {    
            match (Map.get(topic, Option.unwrap(client).subscriptions)) {
                Some(s) => {
                    s.status = true
                },
                None => {
                    Map.set(topic, {
                        status: true, 
                        topicName: topic, 
                        topicLength: length
                    } : MqttSubscription, Option.unwrap(client).subscriptions)
                    void
                }            

            }
        }
    }   

    let unsubscribe = (num: Number, topic: Option<String>, length: Number) => {
        let i = 0
        match (topic) {
            Some(s) =>  {
                let client = Map.get(num, mqttClients)
                if (Option.isSome(client)) {    
                    let k = Map.get(s, Option.unwrap(client).subscriptions)
                    if (Option.isSome(k)) {
                        Option.unwrap(k).status = false
                    }
                }
            },
            None => {
                let client = Map.get(num, mqttClients)
                if (Option.isSome(client)) {    
                    Map.forEach((x, y) => {
                        y.status = false
                    }, Option.unwrap(client).subscriptions)
                }
            }
        }
        void
    }
    
    let numIsIncorrect = (num: Number) => {
        if (num >= _MQTTBROKER_MAX_NUM) {
            true
        } else {
            false
        }
    }

    let clientIsConnected = (num: Number) => {
        let mut result = false

        if (num == _MQTTBROKER_MY_NUM) {
            result = true
        } else if (numIsIncorrect(num)) {
            result = false
        } else {
            let client = Map.get(num, mqttClients)
            if (Option.isSome(client)) {        
                result = Option.unwrap(client).state
            } else {
                result = false
            }
        }

        result
    }


    let _connect = (num: Number) => {
        let client = Map.get(num, mqttClients)
        if (Option.isSome(client)) {        
            Option.unwrap(client).state = true
        } 
    }

    let _disconnect = (num: Number) => {
        if (!numIsIncorrect(num)) {
            unsubscribe(num, None, 0)
            let client = Map.get(num, mqttClients)
            if (Option.isSome(client)) {                
                let kk = Option.unwrap(client)
                kk.state = false
                socketServer.disconnectClient(Option.unwrap(kk.socketClient))
            }
        }
    }

    let _subscribe = (topic: String) => {
        subscribeInternal(_MQTTBROKER_MY_NUM, topic, String.length(topic))
        void
    }

    let _unsubscribe = (topic: String) => {
        unsubscribe(_MQTTBROKER_MY_NUM, Some(topic), String.length(topic))
    }


    let sendAnswer = (num: Number, fixedHeader: Number, fixedHeaderLsb: Number, fixedHeaderRemaining: Number, variableHeader: Bytes, variableHeaderLength: Number, payload: Option<Bytes>, payloadLength: Option<Number>) => {
        let mut return = false
        let mut ii = 0

        schedYield()

        let answer = Bytes.make(fixedHeaderRemaining +2)

        Bytes.setInt8(0, Int32.fromNumber(((fixedHeader << 4) | fixedHeaderLsb)), answer)
        Bytes.setInt8(1, Int32.fromNumber(fixedHeaderRemaining), answer)

        for (let mut i = 2; i < variableHeaderLength + 2; i+=1) {
            Bytes.setInt8(i, Bytes.getInt8U(i-2, variableHeader) ,answer)
            ii = i
        }

        match (fixedHeader) {
            _ when fixedHeader == _CONNACK => debug("[MQTT]["++toString(num)++"][SEND] CONNACK"),
            _ when fixedHeader == _SUBACK => {
                for (let mut i = ii; i < Option.unwrap(payloadLength); i += 1) {
                    Bytes.setInt8(i, Bytes.getInt8U(i-ii,  Option.unwrap(payload)) ,answer)
                }
                debug("[MQTT]["++toString(num)++"][SEND] SUBACK")
                void
            },
            _ when fixedHeader == _UNSUBACK => debug("[MQTT]["++toString(num)++"][SEND] UNSUBACK"),
            _ when fixedHeader == _PINGRESP => debug("[MQTT]["++toString(num)++"][SEND] PINGRESP"),
            _ => {
                return = true
            }
        }
        
        if (!return) {       
            let client = Map.get(num, mqttClients)
            if (Option.isSome(client)) {     
                let k = Option.unwrap(client)
                socketServer.sendBytes(Option.unwrap(k.socketClient),  answer, Bytes.length(answer) )
                void
            }
            void
        }
        void
    }

    let sendMessage = (num: Number, topic: Bytes, payload: Bytes) => {
        let remainingLength = Bytes.length(topic) + Bytes.length(payload) + 2
        let topicLength = Bytes.length(topic)

        let answer = Bytes.make(remainingLength+2)

        Bytes.setInt8(0, Int32.fromNumber((_PUBLISH << 4) | 0x00), answer)
        Bytes.setInt8(1, Int32.fromNumber(remainingLength), answer)
        Bytes.setInt8(2, 0l, answer)
        Bytes.setInt8(3, Int32.fromNumber(topicLength), answer)

        for (let mut i = 4; i < topicLength+4; i += 1) {
            Bytes.setInt8(i, Bytes.getInt8U(i-4, topic), answer)
        }

        for (let mut i = topicLength + 4; i < remainingLength +2 ; i += 1) {
            Bytes.setInt8(i, Bytes.getInt8U(i - topicLength - 4, payload), answer)
        }
        
        let client = Map.get(num, mqttClients)
        if (Option.isSome(client)) {
            socketServer.sendBytes(Option.unwrap(Option.unwrap(client).socketClient),  answer, Bytes.length(answer))
            void
        }
        void
    }
    
    let publishToClient = (topic: Bytes, payload: Bytes, retain: Number) => {        
        if (Bytes.length(topic) <= _MQTTBROKER_TOPIC_MAX_LENGTH) {
            let topicStr = Bytes.toString(topic)

            Map.forEach((k,v) => {
                if (clientIsConnected(k)) {
                    Map.forEach((kk,vv) => {
                        if (v.state) {
                            if (vv.status && compare(topicStr, vv)) {
                                if (k == _MQTTBROKER_MY_NUM) {
                                    callbackEnabled = true
                                } else {
                                    sendMessage(k, topic, payload)
                                }
                            }
                        }
                        void
                    }, v.subscriptions)                        
                }   
                void
            }, mqttClients)

            true
            // TODO: retain
        } else {
            debug("[PUBLISH] Topic length to long!")
            false
        }
        false
    }

    let parsing = (num: Number, payload: String, length: Number) => {
        if (!numIsIncorrect(num)) {
            let b = Bytes.fromString(payload)
            let k = Int32.toNumber(Bytes.getInt8U(0, b)) >> 4

            schedYield()

            match (k) {
                _ when k == _CONNECT  => {
                    let variableHeader = Bytes.make(2);
                    let protocolLevel = String.charCodeAt(8,payload);
                    let connectFlags = String.charCodeAt(9,payload);
                    
                    debug("[CONNECT] " ++ toString(protocolLevel) ++ " - " ++ toString(connectFlags))

                    Bytes.setInt8(0, Int32.fromNumber(0x01 & _SESSION_PRESENT_ZERO), variableHeader)

                    if (protocolLevel == _MQTT_VERSION_3_1_1) {
                        Bytes.setInt8(1, Int32.fromNumber( _CONNECT_ACCEPTED), variableHeader)
                        sendAnswer(num, _CONNACK, 0, 2, variableHeader, 2, None, None)
                        _connect(num)
                    } else {
                        Bytes.setInt8(1, Int32.fromNumber( _CONNECT_REFUSED_UPV), variableHeader)
                        sendAnswer(num, _CONNACK, 0, 2, variableHeader, 2, None, None)
                        _disconnect(num)
                    }

                },
                _ when k == _PUBLISH => {
                    let z = Int32.toNumber(Bytes.getInt8U(0, b)) 
                    let dup = (z >> 3) & 0x1 // TODO
                    let qos = (z >> 1) & 0x3 // TODO
                    let retain = z & 0x1

                    let remainingLength = Int32.toNumber(Bytes.getInt8U(1,b))
                    let mut lengthTopic = Int32.toNumber( Bytes.getInt8S(3,b))

                    let topic = Bytes.toString(Bytes.slice(4,Bytes.length(b) - remainingLength, b))

                    if (lengthTopic > 255) {
                        lengthTopic = _MQTTBROKER_TOPIC_MAX_LENGTH
                    }

                    let mut packetIdentifier = Bytes.make(0)

                    if (qos > 0) {
                        packetIdentifier = Bytes.make(1)
                        Bytes.setInt8(0,Bytes.getInt8U(4+lengthTopic,b), packetIdentifier)
                    }


                    let p = Bytes.slice(4,lengthTopic, b)
                    let m = Bytes.slice(4+Bytes.length(packetIdentifier) + lengthTopic, Bytes.length(b)- (4+Bytes.length(packetIdentifier) + lengthTopic), b)

                    debug("[PUBLISH]["++ toString(num) ++"][DUP "++ toString(dup) ++"][QOS "++ toString(qos) ++"][RETAIN "++ toString(retain) ++"][Topic "++Bytes.toString(p)++"]")

                    publishToClient(p,m, retain  )    

                    match (qos) {
                        _ when qos == 0 => {void},
                        _ when qos == 1 => sendAnswer(num, _PUBACK,0,2, Bytes.make(0), 0, None, None),
                        _ when qos == 2 => {
                            sendAnswer(num, _PUBREC,0,2, Bytes.make(0), 0, None, None);
                            sendAnswer(num, _PUBREL,0,2, Bytes.make(0), 0, None, None);
                            sendAnswer(num, _PUBCOMP,0,2, Bytes.make(0), 0, None, None);
                        },
                        _ => void
                    }

                    if (callbackEnabled) {
                        let client = Map.get(num, mqttClients)
                        if (Option.isSome(client)) {        
                            cbReceive(Option.unwrap(client),Bytes.toString(p),Bytes.toString(m))
                            callbackEnabled = false
                        }
                    }

                    void
                },
                _ when k == _SUBSCRIBE => {
                    let packetIdentifier = msbLsb(Int32.toNumber( Bytes.getInt8U(2,b)))          
                    let lengthMsbLsb = msbLsb(Int32.toNumber( Bytes.getInt8U(4,b)))          
                    let requestedQos = Bytes.slice(6 + lengthMsbLsb,1,b)
                    let topic = Bytes.toString(Bytes.slice(6,Bytes.length(b) - 6, b))

                    debug("[SUBSCRIBE]["++ toString(num) ++"][Paket: "++ toString(packetIdentifier) ++ "][Length: "++ toString(lengthMsbLsb) ++"][Qos: "++ Bytes.toString(requestedQos) ++"][Topic: "++ topic++ "]")
                    subscribeInternal(num, topic, String.length(topic))
                    
                    let result = Bytes.slice(2,2, b)
                    sendAnswer(num, _SUBACK, 0, 3, result, Bytes.length(result), Some(requestedQos), Some(1))  
                    void
                },
                _ when k == _UNSUBSCRIBE => {
                    let packetIdentifier = msbLsb(Int32.toNumber( Bytes.getInt8U(2,b)))          
                    let lengthMsbLsb = msbLsb(Int32.toNumber( Bytes.getInt8U(4,b)))          
                    let topic = Bytes.toString(Bytes.slice(6,Bytes.length(b) - 6, b))
                    let result = Bytes.slice(2,2,b)

                    debug("[UNSUBSCRIBE]["++ toString(num) ++"][Paket: "++ toString(packetIdentifier) ++ "][Length: "++ toString(lengthMsbLsb) ++"][Topic: "++ topic++ "]")
                    sendAnswer(num, _UNSUBACK, 0, 2, result, Bytes.length(result), None, None);
                    void
                },
                _ when k == _PINGREQ => {
                    debug("[PINGREQ]["++ toString(num) ++"]")
                    sendAnswer(num, _PINGRESP,0,2, Bytes.make(0), 0, None, None);
                    void    
                },
                _ when k == _DISCONNECT => {
                    debug("[DISCONNECT]["++ toString(num) ++"]")
                    _disconnect(num);
                    void
                },
                _ => debug("[UNKNOWN]")
            }
        }
        void
    }
 
    let _initialize = () => {
        
        Map.set(_MQTTBROKER_MY_NUM, {
            state: true, 
            socketClient: None,
            subscriptions: Map.make() 
        } : MqttClient, mqttClients)
    
        socketServer.onConnected(
            (socketClient) => {
                let client = Map.get(socketClient.getFileDescriptorNumber(), mqttClients)
                if (Option.isSome(client)) {   
                    Option.unwrap(client).socketClient = Some(socketClient)
                } else {
                    Map.set(socketClient.getFileDescriptorNumber(), {
                        state: false, 
                        socketClient: Some(socketClient),
                        subscriptions: Map.make() 
                    } : MqttClient, mqttClients)
                }
                void
            }
        )


        socketServer.onDisconnected(
            (client) => {
                let found = Map.get(client.getFileDescriptorNumber(), mqttClients)
                if (Option.isSome(found)) {   
                    Map.remove(client.getFileDescriptorNumber(), mqttClients)
                }
                void
            }
        )

        socketServer.onReceive(
            (client, message) => {
                parsing(client.getFileDescriptorNumber(), message, String.length(message))
                void
            }
        )
        void
    }


    let _start = () => {
        socketServer.start()
        void
    }

    let _loop = () => {
        socketServer.accecptClient();
        Server.sleep(3)
        schedYield()
        void
    }

    let _setReceiveCallback = (cb: ((MqttClient, String, String) -> Void)) => {
        cbReceive = cb
    }

    
    let _publish = (topic: String, payload: String,  retain: Bool) => {
        let t = Bytes.fromString(topic)
        let p = Bytes.fromString(payload)
        let mut ret = 0;
        
        if (retain) {
            ret = 1
        }
        publishToClient(t, p, ret)
        void
    }
    // return
    {
        initialize: _initialize,
        start: _start,
        loop: _loop,
        connect: _connect,
        subscribe: _subscribe,
        unsubscribe: _unsubscribe,
        publish: _publish,
        onReceive: _setReceiveCallback,
    }
}
